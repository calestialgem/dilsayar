// SPDX-FileCopyrightText: 2022 Cem Ge√ßgel <gecgelcem@outlook.com>
// SPDX-License-Identifier: GPL-3.0-or-later

output "dilsayar.c";
start *Statement;
skip Whitespace | Comment;

Statement = Output | Start | Skip | Rule;
Output    = "output" String ';';
Start     = "start" Pattern ';';
Skip      = "skip" ?Pattern ';';
Terminal  = "terminal" ';';
Rule      = Identifier '=' Pattern ';';

Pattern = Set
        | NotSet
        | String
        | Reference
        | Group
        | FixedTimes
        | OneOrMore
        | ZeroOrMore
        | Optional
        | Alternatives
        ;

Alternatives = Pattern '|' Pattern;
Optional     = '?' Pattern;
ZeroOrMore   = '*' Pattern;
OneOrMore    = '+' Pattern;
FixedTimes   = Number Pattern;
Group        = '(' +Pattern ')';
Reference    = Identifier;

terminal;

String = '"' *('\\' (2'0~9a~fA~F' | 'tn\\"') | !'\\"') '"';
NotSet = '!' Set;
Set    = '\'' *(Escaped ?('\~' Escaped)) '\'';

Number     = '1~9' *'0~9';
Escaped    = '\\' (2'0~9a~fA~F' | 'tn\\\'\~') | !'\\\'\~';
Identifier = 'A~Z' *'a~zA~Z';
Whitespace = '\t\n ';
Comment    = "//" *!'\n' '\n';
