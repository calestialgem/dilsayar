// SPDX-FileCopyrightText: 2022 Cem Ge√ßgel <gecgelcem@outlook.com>
// SPDX-License-Identifier: GPL-3.0-or-later

output "dilsayar.c";

skip Whitespace | Comment;

start *Statement;

Statement = Output | Start | Skip | Rule;
Output    = "output" String ';'; // Path to output C file.
Start     = "start" Pattern ';'; // Pattern of start symbol.
Skip      = "skip" Pattern ';';  // Pattern to skip between justapositioned.
Rule      = Identifier '=' Pattern ';';

Pattern      = Alternatives;

Alternatives = Repeat *('|' Repeat);

Repeat     = Group | Optional | ZeroOrMore | OneOrMore | FixedTimes;
Optional   = '?' Group;
ZeroOrMore = '*' Group;
OneOrMore  = '+' Group;
FixedTimes = Number Group;

Group = Literal | '(' +Literal ')';

skip;

Number = '1~9' *'0~9';

Literal   = Set | NotSet | AllSet | String | Reference;
Set       = '\'' +(Escaped | Escaped '\~' Escaped) '\'';
NotSet    = '!' Set;
AllSet    = '.';
String    = '"' *('\\' (2'0~9a~fA~F' | 'tn\\"') | !'\\"') '"';
Reference = Identifier;

// Backslash fallowed by two hex digits or;
// tab, newline, backslash, single quote, and tilde.
// Otherwise it is not escaped;
// anything other than backslash, single quote, and tilde is accepted.
Escaped = '\\' (2'0~9a~fA~F' | 'tn\\\'\~') | !'\\\'\~';

Identifier = 'A~Z' *'a~zA~Z'; // PascalCase.

// Carrige return is handled by the program.
Whitespace = '\t\n ';

Comment    = "//" *!'\n' '\n';
