// SPDX-FileCopyrightText: 2022 Cem Ge√ßgel <gecgelcem@outlook.com>
// SPDX-License-Identifier: GPL-3.0-or-later

skip  Whitespace | Comment;
start *Statement;

Statement = Skip | Start | Rule;
Skip      = "skip" ?Pattern ';';
Start     = "start" Pattern ';';
Rule      = Identifier '=' Pattern ';';

Pattern     = Alternative *('|' Alternative);
Alternative = +Unit;

Unit = Set
     | NotSet
     | String
     | Reference
     | Group
     | FixedTimes
     | OneOrMore
     | ZeroOrMore
     | Optional;

Optional   = '?' Unit;
ZeroOrMore = '*' Unit;
OneOrMore  = '+' Unit;
FixedTimes = Number Unit;
Group      = '(' Pattern ')';
Reference  = Identifier;

skip;

String = '"' *('\\' (2'0~9a~fA~F' | 'tn\\"') | !'\\"') '"';
NotSet = '!' Set;
Set    = '\'' *(Escaped ?('\~' Escaped)) '\'';

Number     = '1~9' *'0~9';
Escaped    = '\\' (2'0~9a~fA~F' | 'tn\\\'\~') | !'\\\'\~';
Identifier = 'A~Z' *'a~zA~Z';
Whitespace = '\t\n ';
Comment    = "//" *!'\n' '\n';
