// SPDX-FileCopyrightText: 2022 Cem Ge√ßgel <gecgelcem@outlook.com>
// SPDX-License-Identifier: GPL-3.0-or-later

#output "dilsayar.c";
#start *statement;
#skip whitespace | comment;

statement = output | start | skip | rule;
output    = "#output" string ';';
start     = "#start" pattern ';';
skip      = "#skip" pattern ';';
rule      = identifier '=' pattern ';';

pattern      = alternatives;
alternatives = repeat *('|' repeat);

repeat       = group | optional | zero_or_more | one_or_more | fixed_times;
optional     = '?' group;
zero_or_more = '*' group;
one_or_more  = '+' group;
fixed_times  = '1~9' +'0~9' group;

group = literal | '(' +literal ')';

#skip;

literal   = set | notset | allset | string | reference;
set       = '\'' +(escaped | !'\'') '\'';
notset    = '!' set;
allset    = '.';
string    = '"' *(escaped | !'"') '"';
reference = identifier;

escaped = '\\' (2'0~9a~zA~Z' | 'tn\\\'"\~');

identifier = 'a~zA~Z' *'a~zA~Z0~9_';

whitespace = '\t\n ';
comment    = "//" *. '\n';
